enable_testing()
include(GoogleTest)
find_package(GTest MODULE REQUIRED)

file(GLOB_RECURSE
  ALLTESTS_SOURCES
  CONFIGURE_DEPENDS
  "${CMAKE_CURRENT_SOURCE_DIR}/src/test_*.cpp"
)

set(TESTS_TARGET alltests)
add_executable(${TESTS_TARGET})
target_sources(${TESTS_TARGET}
  PRIVATE
    ${ALLTESTS_SOURCES}
)
target_include_directories(${TESTS_TARGET}
  PRIVATE
    "$<BUILD_INTERFACE:${PROJECT_BINARY_DIR};${PROJECT_SOURCE_DIR}/src>"
)
target_link_libraries(${TESTS_TARGET}
  PRIVATE
    ${PROJECT_TARGET}
    fmt::fmt
    GTest::GTest
    GTest::Main
    commons::commons
)
set_target_properties(${TESTS_TARGET}
  PROPERTIES
    DEBUG_POSTFIX d
    CXX_EXTENSIONS NO
    RUNTIME_OUTPUT_DIRECTORY ${PROJECT_RUNTIME_OUTPUT_DIR}
    LIBRARY_OUTPUT_DIRECTORY ${PROJECT_LIBRARY_OUTPUT_DIR}
    ARCHIVE_OUTPUT_DIRECTORY ${PROJECT_ARCHIVE_OUTPUT_DIR}
)

gtest_discover_tests(${TESTS_TARGET}
  WORKING_DIRECTORY ${PROJECT_RUNTIME_OUTPUT_DIR}
)
